from build_config import *

Import("""
    target_arch
    build_flavor
    lib_path
    lib_suffix
    qt_lib_path
    platform
    SetupUtMocs
    rms_env
""")

Import("""
    common_obj
    crypto_api_obj
    crypto_obj
    crypto_platform_crypto_obj
    crypto_platform_keystorage_obj
    crypto_platform_logger_obj
    crypto_platform_settings_obj
    googletest_obj
    json_obj
    restclients_obj
    rms_platform_filesystem_obj
    rms_platform_http_obj
    rms_platform_json_obj
    rms_platform_logger_obj
    rms_platform_settings_obj
    rms_platform_xml_obj
""")

src_files = Glob('*.cpp')
srcdir = Dir('.').srcnode().abspath

SetupUtMocs(srcdir, build_flavor)

src_files += Glob(build_flavor + '/*.cpp')

rms_env.Append(CPPPATH='#sdk/rmscrypto_sdk/CryptoAPI')

rms_env.Append(CPPDEFINES = { 'SRCDIR' : "\\\"" + srcdir + "/\\\"" })

objects = [
    common_obj,
    crypto_api_obj,
    crypto_obj,
    crypto_platform_crypto_obj,
    crypto_platform_keystorage_obj,
    crypto_platform_logger_obj,
    crypto_platform_settings_obj,
    googletest_obj,
    json_obj,
    restclients_obj,
    rms_platform_filesystem_obj,
    rms_platform_http_obj,
    rms_platform_json_obj,
    rms_platform_logger_obj,
    rms_platform_settings_obj,
    rms_platform_xml_obj,
]
libs = [
    'Qt5Core' + lib_suffix,
    'Qt5Network' + lib_suffix,
    'Qt5Xml' + lib_suffix,
    'Qt5XmlPatterns' + lib_suffix,
    'Qt5Test' + lib_suffix,
    'Qt5Gui' + lib_suffix,
]

if platform == 'win32':
  libs += [    
    'libeay32',
    'ssleay32',
    'Advapi32',
    'Gdi32',
    'User32',
    'DnsAPI',
  ]
elif platform == 'linux2':
  libs += [
    'ssl',
    'crypto',
    'secret-1',
    'glib-2.0',
    'pthread',
    'GL',
    'resolv',
  ]

rest_clients_ut_obj = rms_env.Object(source = src_files)
rest_clients_test = rms_env.Program(target = "rest_clients_ut", 
    source = [rest_clients_ut_obj] + objects, LIBS = [libs], LIBPATH = lib_path)
Return('rest_clients_test')