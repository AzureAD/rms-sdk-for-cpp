Import('file_env bins')

Import("""
    googlemock_lib
    googletest_lib
    file_lib
    platform
    libxml2_lib
""")

file_ut_env = file_env.Clone()
install_path = "#" + bins + '/sdk/file_sdk'

src_files = ['unittest_main.cpp', 'test_utils.cpp']
src_files += Glob('../../OPCFileFormat/test/*.cpp')

Install(install_path, Dir('.').srcnode().Dir('../../OPCFileFormat/test/Resources'))

lib_path = []
libs = [
    googlemock_lib,
    googletest_lib,
    file_lib,
    libxml2_lib,
]

if platform == 'win32':
    file_ut_env.Append(CPPPATH='#third_party/include/Libgsf')
    lib_gsf_path = '#third_party/lib/Libgsf'
    lib_path += [
        lib_gsf_path
    ]
    libs += [
        'libgsf-1-114',
        'zlib1',
        'libgobject-2.0-0',
        'libglib-2.0-0',
        'libbz2-1'
    ]
    Install(install_path, Glob(lib_gsf_path + '/*.dll'))
elif platform == 'linux2':
    file_ut_env.ParseConfig('pkg-config --cflags --libs libgsf-1')
    libs += file_ut_env['LIBS']
    libs += [
        'pthread',
    ]

file_sdk_unittest_obj = file_ut_env.Object(source = src_files);
file_sdk_unittest_exe = file_ut_env.Program("file_sdk_unittest", source = file_sdk_unittest_obj, LIBS = libs, LIBPATH = lib_path)
Return('file_sdk_unittest_exe')
